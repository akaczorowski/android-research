apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    signingConfigs {
        release {
            keyAlias 'xxx5'
            keyPassword 'xxx'
            storeFile file('keystoreXXX5')
            storePassword 'xxx'
        }
        debug {
            keyAlias 'xxx1'
            keyPassword 'xxx'
            storeFile file('keystoreXXX1')
            storePassword 'xxx'
        }
    }

    // faster builds see also gradle.properties file for more RAM setting
    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "3g"
    }
    defaultConfig {
        applicationId "akaczorowski.pl.android.research"
        minSdkVersion 18
        targetSdkVersion 22
        versionCode buildVersionCode()
        versionName version + buildName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // only english language
        resConfigs "en"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }

    // http://stackoverflow.com/questions/20673625/android-gradle-plugin-0-7-0-duplicate-files-during-packaging-of-apk
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def fileName = "MyApp"
            if (variant.buildType.name == "release") {
                fileName += "-${variant.versionName}.apk"
            } else {
                fileName += "-${variant.versionName}-${variant.buildType.name}.apk"
            }
            output.outputFile = new File(output.outputFile.parent, fileName)
        }
    }
}

dependencies {

    // examples
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/someJar.jar')
    provided project(':com.symbol.emdk')

    // top level build.gradle should declare libs folder as repo
    compile(name: 'someAArlib', ext: 'aar')
    //
    compile 'com.android.support:support-vector-drawable:23.3.0'
    //logback for android
    compile 'com.github.tony19:logback-android-core:1.1.1-4'
    compile 'com.github.tony19:logback-android-classic:1.1.1-4'
    compile 'org.slf4j:slf4j-api:1.7.6'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    //
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
